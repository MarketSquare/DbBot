*** Settings ***
Library   OperatingSystem
Library   ../libraries/RobotSqliteDatabase.py
Resource  ../resources/commandline.txt
Resource  ../resources/database.txt


*** Variables ***
${test_run}                 ${CURDIR}${/}..${/}testdata${/}one_suite${/}output.xml
${latter_test_run}          ${CURDIR}${/}..${/}testdata${/}one_suite${/}output_latter.xml
${test_run_with_subsuites}  ${CURDIR}${/}..${/}testdata${/}multiple${/}output.xml


*** Keywords ***
Parse Without Keywords ${files}
    Remove Database
    Run  ${program_path} --files ${files}
    Connect To Database  ${default_database}

Parse With Keywords ${files}
    Remove Database
    Run  ${program_path} --files ${files} --also-keywords
    Connect To Database  ${default_database}

Disconnect And Cleanup
    Close Connection
    Remove Database  ${default_database}

Should Have ${n} Test Runs
    Row Count Is Equal To  ${n}  test_runs  ${default_database}

Should Have ${n} Test Run Statuses
    Row Count Is Equal To  ${n}  test_run_status  ${default_database}

Should Have ${n} Test Run Errors
    Row Count Is Equal To  ${n}  test_run_errors  ${default_database}

Should Have ${n} Suites
    Row Count Is Equal To  ${n}  suites  ${default_database}

Should Have ${n} Suite Statuses
    Row Count Is Equal To  ${n}  suite_status  ${default_database}

Should Have ${n} Tests
    Row Count Is Equal To  ${n}  tests  ${default_database}

Should Have ${n} Test Statuses
    Row Count Is Equal To  ${n}  test_status  ${default_database}

Should Have ${n} Tags
    Row Count Is Equal To  ${n}  tags  ${default_database}

Should Have ${n} Tag Statuses
    Row Count Is Equal To  ${n}  tag_status  ${default_database}

Should Have ${n} Keywords
    Row Count Is Equal To  ${n}  keywords  ${default_database}

Should Have ${n} Keyword Statuses
    Row Count Is Equal To  ${n}  keyword_status  ${default_database}

Should Have ${n} Arguments
    Row Count Is Equal To  ${n}  arguments  ${default_database}

Should Have ${n} Messages
    Row Count Is Equal To  ${n}  messages  ${default_database}

Should Have Suites and Tests
    Should Have 1 Test Runs
    Should Have 2 Test Run Statuses
    Should Have 0 Test Run Errors
    Should Have 1 Suites
    Should Have 1 Suite Statuses
    Should Have 19 Tests
    Should Have 19 Test Statuses
    Should Have 57 Tags
    Should Have 3 Tag Statuses

Should Have Keywords
    Should Have 39 Keywords
    Should Have 39 Keyword Statuses
    Should Have 131 Arguments
    Should Have 87 Messages

Should Not Store Keywords
    Should Have 0 Keywords
    Should Have 0 Keyword Statuses
    Should Have 0 Arguments
    Should Have 0 Messages


*** Test Cases ***
Single test run results
    [Setup]  Parse Without Keywords ${test_run}
    Should Have 1 Test Runs
    [Teardown]  Disconnect And Cleanup

Single test run multiple times
    [Setup]  Parse Without Keywords ${test_run} ${test_run}
    Should Have 1 Test Runs
    [Teardown]  Disconnect And Cleanup

Multiple test runs
    [Setup]  Parse Without Keywords ${test_run} ${latter_test_run}
    Should Have 2 Test Runs
    [Teardown]  Disconnect And Cleanup

Single test run without keywords
    [Setup]  Parse Without Keywords ${test_run}
    Should Have Suites And Tests
    Should Not Store Keywords
    [Teardown]  Disconnect And Cleanup

Single test run with keywords
    [Setup]  Parse With Keywords ${test_run}
    Should Have Suites And Tests
    Should Have Keywords
    [Teardown]  Disconnect And Cleanup

Single test run with subsuites
    [Setup]  Parse With Keywords ${test_run_with_subsuites}
    Should Have 1 Test Runs
    Should Have 2 Test Run Statuses
    Should Have 0 Test Run Errors
    Should Have 4 Suites
    Should Have 4 Suite Statuses
    Should Have 50 Tests
    Should Have 50 Test Statuses
    Should Have 150 Tags
    Should Have 3 Tag Statuses
    Should Have 41 Keywords
    Should Have 41 Keyword Statuses
    Should Have 139 Arguments
    Should Have 176 Messages
    [Teardown]  Disconnect And Cleanup

Multiple test runs with the same root suite
    [Setup]  Parse With Keywords ${test_run} ${latter_test_run}
    Should Have ${2*1} Test Runs
    Should Have ${2*2} Test Run Statuses
    Should Have 0 Test Run Errors
    Should Have 1 Suites
    Should Have 2 Suite Statuses
    Should Have 19 Tests
    Should Have ${2*19} Test Statuses
    Should Have 57 Tags
    Should Have ${2*3} Tag Statuses
    Should Have 39 Keywords
    Should Have ${2*39} Keyword Statuses
    Should Have 131 Arguments
    Should Have 99 Messages
    [Teardown]  Disconnect And Cleanup

